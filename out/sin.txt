# precombat
apply_poison
flask
augmentation
food
snapshot_stats
var trinket_sync_slot = 1 if trinket.1.has_stat.any_dps & (!trinket.2.has_stat.any_dps | trinket.1.cooldown.duration >= trinket.2.cooldown.duration) & !trinket.2.is.witherbarks_branch | trinket.1.is.witherbarks_branch
var trinket_sync_slot = 2 if trinket.2.has_stat.any_dps & (!trinket.1.has_stat.any_dps | trinket.2.cooldown.duration > trinket.1.cooldown.duration) & !trinket.1.is.witherbarks_branch | trinket.2.is.witherbarks_branch
var effective_spend_cp = cp_max_spend - 2 < ?5 * talent.hand_of_fate
stealth
slice_and_dice,precombat_seconds = 1

# actions
stealth
kick
var single_target = targets.fan_of_knives < 2
var regen_saturated = energy.regen_combined > 35
var not_pooling = (deathmark.ticking | kingsbane.ticking | shiv.up) | (envenom.up & envenom.remains <= 1) | energy.pct >= (40 + 30 * talent.hand_of_fate - 15 * talent.vicious_venoms) | fight_remains <= 20
call_action_list,name = stealthed if stealthed.rogue | stealthed.improved_garrote | master_assassin_remains > 0
slice_and_dice if !slice_and_dice.up & rupture.ticking & cp >= 1 & (!indiscriminate_carnage.up | single_target)
envenom if slice_and_dice.up & slice_and_dice.remains < 5 & cp >= 5
call_action_list,name = cds
call_action_list,name = core_dot
call_action_list,name = aoe_dot if !single_target
call_action_list,name = direct
arcane_torrent if energy.deficit >= 15 + energy.regen_combined
arcane_pulse
lights_judgment
bag_of_tricks

# aoe_dot
var scent_effective_max_stacks = (targets.fan_of_knives * talent.scent_of_blood.rank * 2) > ?20
var scent_saturation = scent_of_blood.stack >= scent_effective_max_stacks
var dot_finisher_condition = cp >= effective_spend_cp & (pmultiplier <= 1)
crimson_tempest,target_if = min:remains if targets >= 3 & dot_finisher_condition & refreshable & energy.regen_combined > 25 & !cooldown.deathmark.ready & target.time_to_die - remains > 6
garrote,cycle_targets = 1 if cp.deficit >= 1 & (pmultiplier <= 1) & refreshable & !regen_saturated & target.time_to_die - remains > 12
rupture,cycle_targets = 1 if dot_finisher_condition & refreshable & (!regen_saturated & (talent.scent_of_blood.rank = 2 | talent.scent_of_blood.rank <= 1 & (indiscriminate_carnage.up | target.time_to_die - remains > 15))) & target.time_to_die - remains > (4 + (talent.dashing_scoundrel * 5) + (regen_saturated * 6)) & !darkest_night.up
rupture,cycle_targets = 1 if dot_finisher_condition & refreshable & regen_saturated & !scent_saturation & target.time_to_die - remains > 15 & !darkest_night.up
garrote if refreshable & cp.deficit >= 1 & (pmultiplier <= 1 | remains <= tick_time & targets.fan_of_knives >= 3) & (remains <= tick_time * 2 & targets.fan_of_knives >= 3) & (target.time_to_die - remains) > 4 & master_assassin_remains = 0

# cds
var deathmark_ma_condition = !talent.master_assassin.enabled | garrote.ticking
var deathmark_kingsbane_condition = !talent.kingsbane | cooldown.kingsbane.remains <= 2
var deathmark_condition = !stealthed.rogue & slice_and_dice.remains > 5 & rupture.ticking & envenom.up & !deathmark.up & deathmark_ma_condition & deathmark_kingsbane_condition
call_action_list,name = items
invoke_external_buff,name = power_infusion if deathmark.ticking
deathmark if (deathmark_condition & target.time_to_die >= 10) | fight_remains <= 20
call_action_list,name = shiv
kingsbane if (shiv.up | cooldown.shiv.remains < 6) & envenom.up & (cooldown.deathmark.remains >= 50 | deathmark.ticking) | fight_remains <= 15
thistle_tea if !thistle_tea.up & (((energy.deficit >= 100 + energy.regen_combined | charges >= 3) & shiv.remains >= 4) | targets.fan_of_knives >= 4 & shiv.remains >= 6) | fight_remains < charges * 6
call_action_list,name = misc_cds
call_action_list,name = vanish if !stealthed.all & master_assassin_remains = 0
cold_blood if cp >= effective_spend_cp & !rupture.refreshable & !edge_case.up & !darkest_night.up & cooldown.deathmark.remains > 10

# core_dot
garrote if cp.deficit >= 1 & (pmultiplier <= 1) & refreshable & target.time_to_die - remains > 12
rupture if cp >= effective_spend_cp & (pmultiplier <= 1) & refreshable & target.time_to_die - remains > (4 + (talent.dashing_scoundrel * 5) + (regen_saturated * 6)) & !darkest_night.up

# direct
envenom if !darkest_night.up & cp >= effective_spend_cp & (not_pooling | amplifying_poison.stack >= 20 | cp > cp_max_spend | !single_target) & !vanish.up
envenom if darkest_night.up & cp >= cp_max_spend
var use_filler = cp.deficit > 1 | not_pooling | !single_target
var use_caustic_filler = talent.caustic_spatter & rupture.ticking & (!caustic_spatter.up | caustic_spatter.remains <= 2) & cp.deficit > 1 & !single_target
mutilate if use_caustic_filler
ambush if use_caustic_filler
echoing_reprimand if use_filler | fight_remains < 20
fan_of_knives if use_filler & !priority_rotation & (targets.fan_of_knives >= 3 | clear_the_witnesses.up & !talent.vicious_venoms)
fan_of_knives,target_if = !deadly_poison_ticking & (!priority_rotation | garrote.ticking | rupture.ticking) if use_filler & targets.fan_of_knives >= 3
ambush if use_filler & (blindside.up | stealthed.rogue) & (!kingsbane.ticking | deathmark.down | blindside.up)
mutilate,target_if = !deadly_poison_ticking & !amplifying_poison.up if use_filler & targets.fan_of_knives = 2
mutilate if use_filler

# items
var base_trinket_condition = rupture.ticking & cooldown.deathmark.remains < 2 | fight_remains <= 22
use_item,name = ashes_of_the_embersoul,use_off_gcd = 1 if (kingsbane.ticking & kingsbane.remains <= 11) | fight_remains <= 22
use_item,name = algethar_puzzle_box,use_off_gcd = 1 if base_trinket_condition
use_item,name = treacherous_transmitter,use_off_gcd = 1 if base_trinket_condition
do_treacherous_transmitter_task,use_off_gcd = 1 if deathmark.ticking | fight_remains <= 20
use_item,name = imperfect_ascendancy_serum,use_off_gcd = 1 if base_trinket_condition
use_items,slots = trinket1 if (trinket_sync_slot = 1 & (deathmark.up | fight_remains <= 20) | (trinket_sync_slot = 2 & (!trinket.2.cooldown.ready | !deathmark.up & cooldown.deathmark.remains > 20)) | !trinket_sync_slot)
use_items,slots = trinket2 if (trinket_sync_slot = 2 & (deathmark.up | fight_remains <= 20) | (trinket_sync_slot = 1 & (!trinket.1.cooldown.ready | !deathmark.up & cooldown.deathmark.remains > 20)) | !trinket_sync_slot)

# misc_cds
potion if bloodlust.react | fight_remains < 30 | deathmark.up
blood_fury if deathmark.up
berserking if deathmark.up
fireblood if deathmark.up
ancestral_call if (!talent.kingsbane & deathmark.up & shiv.up) | (talent.kingsbane & deathmark.up & kingsbane.ticking & kingsbane.remains < 8)

# shiv
var shiv_condition = !shiv.up & garrote.ticking & rupture.ticking
var shiv_kingsbane_condition = talent.kingsbane & envenom.up & shiv_condition
shiv if talent.arterial_precision & shiv_condition & targets.fan_of_knives >= 4 & crimson_tempest.ticking | fight_remains <= charges * 8
shiv if !talent.lightweight_shiv.enabled & shiv_kingsbane_condition & (kingsbane.ticking & kingsbane.remains < 8 | cooldown.kingsbane.remains >= 24) & (!talent.crimson_tempest.enabled | single_target | crimson_tempest.ticking) | fight_remains <= charges * 8
shiv if talent.lightweight_shiv.enabled & shiv_kingsbane_condition & (kingsbane.ticking | cooldown.kingsbane.remains <= 1) | fight_remains <= charges * 8
shiv if talent.arterial_precision & shiv_condition & deathmark.up | fight_remains <= charges * 8
shiv if !talent.kingsbane & !talent.arterial_precision & shiv_condition & (!talent.crimson_tempest.enabled | single_target | crimson_tempest.ticking) | fight_remains <= charges * 8

# stealthed
pool_resource,for_next = 1
ambush if !deathstalkers_mark.up & talent.deathstalkers_mark & !darkest_night.up
shiv if talent.kingsbane & (kingsbane.ticking | cooldown.kingsbane.up) & (!shiv.up & shiv.remains < 1) & envenom.up
envenom if cp >= effective_spend_cp & kingsbane.ticking & envenom.remains <= 3
envenom if cp >= effective_spend_cp & master_assassin_aura.up & single_target
rupture,target_if = cp >= effective_spend_cp & indiscriminate_carnage.up & refreshable & (!regen_saturated | !scent_saturation | !rupture.ticking)
garrote,target_if = min:remains if stealthed.improved_garrote & (remains < 12 | pmultiplier <= 1 | (indiscriminate_carnage.up & active_garrote < targets.fan_of_knives)) & !single_target & target.time_to_die - remains > 2
garrote if stealthed.improved_garrote & (pmultiplier <= 1 | remains < 12 | !single_target & master_assassin_aura.remains < 3) & cp.deficit >= 1 + 2 * talent.shrouded_suffocation

# vanish
pool_resource,for_next = 1,extra_amount = 45
vanish if !fatebound_lucky_coin.up & (fatebound_coin_tails.stack >= 5 | fatebound_coin_heads.stack >= 5)
vanish if !talent.master_assassin & !talent.indiscriminate_carnage & talent.improved_garrote & cooldown.garrote.up & (garrote.pmultiplier <= 1 | garrote.refreshable) & (deathmark.up | cooldown.deathmark.remains < 4) & cp.deficit >= (targets.fan_of_knives > ?4)
pool_resource,for_next = 1,extra_amount = 45
vanish if !talent.master_assassin & talent.indiscriminate_carnage & talent.improved_garrote & cooldown.garrote.up & (garrote.pmultiplier <= 1 | garrote.refreshable) & targets.fan_of_knives > 2 & (target.time_to_die - remains > 15 | raid_event.adds.in > 20)
vanish if !talent.improved_garrote & talent.master_assassin & !rupture.refreshable & garrote.remains > 3 & deathmark.up & (shiv.up | deathmark.remains < 4)
vanish if talent.improved_garrote & cooldown.garrote.up & (garrote.pmultiplier <= 1 | garrote.refreshable) & (deathmark.up | cooldown.deathmark.remains < 4) & raid_event.adds.in > 30

